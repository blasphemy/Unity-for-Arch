# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-appmenu
_actual_ver=13.01.0
_extra_ver=+13.10.20130930
_translations=20130418
pkgver=${_actual_ver}daily13.09.30
pkgrel=101
pkgdesc="An indicator to host the menus from an application"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-appmenu"
license=('GPL')
groups=('unity')
depends=('dconf' 'libappindicator3' 'libindicator3' 'libdbusmenu-gtk3' 'bamf')
makedepends=('gtk-doc' 'intltool' 'vala')
optdepends=('unity-gtk-module: support for GTK+ 2 and GTK+ 3 apps'
            'appmenu-qt: support for Qt apps')
provides=('indicator-appmenu-tools')
conflicts=('indicator-appmenu-tools')
options=('!libtool' '!emptydirs')
install="${pkgname}.install"
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-appmenu_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-appmenu.tar.gz"
        '0001_autoconf.patch')
sha512sums=('e1dbb8e8180dabcddda63798671d70b5800b2879ce5c436404aaf8ca042af771840b2e90fd73cbd1c154663d278fd79cd07e4fcea74eb5814e26500484e6932a'
            '68f2bab05620de5bb7879a8a6ecf94636fe3302b8087eac57c8e48e02a904e1f8ee9e109682f365f1990d23e92176f6f139aa6ecdc10fec84f4884758ac37474'
            '31b83e433824bd7acd7be93bfd60932114eff344fea77c8ea1125272d5881fd61c1609d4fbf24de0cc8838e76d8ac405e7a19162e3ce2021b6b5b6dbb61c31f8')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Autoconf fix
  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  gtkdocize
  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
