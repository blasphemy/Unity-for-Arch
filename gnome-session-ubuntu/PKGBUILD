# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-session-ubuntu
_ppa_rel=0ubuntu1~saucy1
_translations=20130418
pkgver=3.10.0
pkgrel=101
pkgdesc="The GNOME Session Handler"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('systemd' 'dconf' 'gsettings-desktop-schemas' 'gtk3' 'gnome-desktop' 'hicolor-icon-theme' 'json-glib' 'libgl' 'libsm' 'libxtst' 'upower')
makedepends=('gnome-common' 'gtk-doc' 'intltool' 'mesa' 'xtrans')
groups=('gnome')
provides=("gnome-session=${pkgver}")
conflicts=('gnome-session')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-session/gnome-session_${pkgver}-${_ppa_rel}.debian.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-session.tar.gz"
        '55gnome-session_gnomerc'
        'timeout.patch'
        'polkit-gnome-authentication-agent-1-unity.desktop')
sha512sums=('036adc7ecff7f5aa1319bfac636b6b77925c689cb92880224cf665456dca5a55299025971a2bcdb841e27334ddc0cd6125241b39e48a30c3cc0b02c22612f5c9'
            'ab4311c3d0ee2d5a281fcfcd805f96cde1ce7f21bc08ff553fadae7dade6f044dedf356ce6f52f6bdc819e53a54d08111383f7075a7341c5bed6caaaa3343907'
            'f65eb708d47ab132068834ff150219702b871f8ebbb5c308e52d6fbc5aa49fbe2e10dbf32e54674ffc2ee853d0248f01118f6692cba47b1d7b787abfbdbda75f'
            '9246e9bcb25493d169064a11cd0609c565e7173464fb594c8feee281c196127af604a8f2b3b8d07edb82ea35bfd3bc5b586719676ec868ee25c102039cb5415a'
            '81d28b8f345c3a16dbb443ee8f195f802b572941aeac2dafaf6ea6b231ccb89a280aa855d09744dfc7ef2ad794a5e7b0002b54b14465865441367055514bcb70'
            '34e32d1c3fd7c417b5dc70737b9dff9c9b0b660a6c99cb8c29e944818df0cb643fcac01c6c5252849bb8595a348aaf4d2a24985560c6b8a9653544e146e5b4af')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Increase timeout, for slow machines
  patch -p1 -i "${srcdir}/timeout.patch"

  # Disable patches
    # We're not Ubuntu, do not hide stuff
      sed -i '/20_hide_nodisplay.patch/d' "${srcdir}/debian/patches/series"
    # Arch Linux does not have sessionmigration
      sed -i '/53_add_sessionmigration.patch/d' "${srcdir}/debian/patches/series"

  # Fix patches
    # Change session name to 'Unity'
      sed -i 's/+_Name=Ubuntu/+_Name=Unity/g' "${srcdir}/debian/patches/50_ubuntu_sessions.patch"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying patch ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/gnome-session-3.0-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnome-session \
    --disable-schemas-compile \
    --enable-systemd \
    --enable-ipv6 # From debian/rules

  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install Ubuntu files
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  #install -m755 "${srcdir}/debian/55gnome-session_gnomerc" "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  # Install my version of the script (more platform-agnostic)
  install -m755 "${srcdir}/55gnome-session_gnomerc" "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  # symlink session files so that KDM can find them
  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -s /usr/share/xsessions/ubuntu.desktop "${pkgdir}/usr/share/apps/kdm/sessions/"

  install -dm755 "${pkgdir}/etc/xdg/autostart/"
  install -m644 "${srcdir}/polkit-gnome-authentication-agent-1-unity.desktop" \
    "${pkgdir}/etc/xdg/autostart/"
}

# vim:set ts=2 sw=2 et:
