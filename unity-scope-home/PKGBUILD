# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-scope-home
pkgver=6.8.0daily13.06.19
pkgrel=1
pkgdesc="Home scope that aggregates results from multiple scopes"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-scope-home"
license=('GPL')
groups=('unity')
depends=('dee' 'glib2' 'json-glib' 'libgee06' 'libsoup' 'libunity' 'lsb-release')
makedepends=('gnome-common' 'intltool' 'vala')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-scope-home_${pkgver}.orig.tar.gz")
sha512sums=('865965518c22754c5aae5593814a8efb18b27a70892ebab2a30335e87bb636ca45d4acfa879dc8fdb2cc2d9f2dd36d6667ca602bd34f3b225e0336685beb3b9b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/unity-scope-home
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check || :
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
