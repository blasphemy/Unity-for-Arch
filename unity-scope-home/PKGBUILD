# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-scope-home
_actual_ver=6.8.1
_extra_ver=+13.10.20130705
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="Home scope that aggregates results from multiple scopes"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-scope-home"
license=('GPL')
groups=('unity')
depends=('dee' 'glib2' 'json-glib' 'libgee06' 'libsoup' 'libunity' 'lsb-release')
makedepends=('gnome-common' 'intltool' 'vala')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-scope-home_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('24ad9a4d7a8b5cb05f9fe869d8976fad7d5f6e1162c396842b63a1a7c247d648a1773eabfc6d1115d770ef1da1ba55d2eaf666d10620082b3e43c64af1f313f1')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/unity-scope-home
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make check || :
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}
