# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-scope-home
_actual_ver=6.8.2
_extra_ver=+13.10.20131003
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=2
pkgdesc="Home scope that aggregates results from multiple scopes"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-scope-home"
license=('GPL')
groups=('unity')
depends=('dee' 'glib2' 'json-glib' 'libgee06' 'libsoup' 'libunity' 'lsb-release')
makedepends=('gnome-common' 'intltool' 'vala')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-scope-home_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('9ec83a939c1109a5e65536e335288c0f39f4232c0795e4ab2ab63dcb23579e66299cc822a45f0b15059a901b0b26836f6fafc7225a89c7911440f49099b59f97')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/unity-scope-home
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}
