# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=indicator-bluetooth
_actual_ver=0.0.6
_extra_ver=+13.10.20131016
_translations=20130418
pkgver=${_actual_ver}.daily13.10.16
pkgrel=100
pkgdesc="Bluetooth status indicator"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-bluetooth"
license=('GPL')
groups=('unity')
# Soft dependency
depends=('gnome-bluetooth')
makedepends=('gnome-common' 'intltool' 'vala')
# We don't want two bluetooth indicators
conflicts=('gnome-bluetooth-ubuntu')
options=('!libtool')
# Ubuntu finally using xz sources? It's a miracle!
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-bluetooth_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-bluetooth.tar.gz"
        '0001_bluetooth_browse_address_Removed.patch'
        '0002_Not_a_phone.patch')
sha512sums=('099064520ad8896b74fc4e85e1119dfa260b987ae70d8a89cd51efe63b128edde5da1d1a5f92efdf2f5fdccb771ed5e030408bcb242e362c8e62e312b56ed62f'
            '5d279bcb9dbb7bb89a127025418bf3326026379630f0c0bbea9b5fe0cad93731d72f91641ea98c5c1fc71d6355855d083ff195f14d3ad73e5f573780c627bd51'
            'c44735821c4468dcf249fae44d8fed936c2f60cb9f6545b12b476509750a95941d7697e5c288d0d06d92fcb84fb5031a3455cf910a263b725b15fddf416d1f58'
            '2b03d729930bcfc6aaeb8ca32226754e77089d1f791c1a2011f1ed487db4295675c8dcc8b924d9bb5426d4b16d34a9471d864e8b9ae8f85e544f8fc8074b4dc4')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # bluetooth_browse_address() was removed in gnome-bluetooth 3.8, so don't link
  # against it
  #patch -p1 -i "${srcdir}/0001_bluetooth_browse_address_Removed.patch"
  patch -p1 -i "${srcdir}/0002_Not_a_phone.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/indicator-bluetooth --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
