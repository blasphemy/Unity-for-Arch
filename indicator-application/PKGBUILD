# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-application
_actual_ver=12.10.1
_extra_ver=+14.04.20131125
pkgver=${_actual_ver}daily13.11.25
pkgrel=101
pkgdesc="A library and indicator to take menus from applications and place them in the panel"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-application"
license=('GPL')
depends=('dbus-glib' 'libappindicator3' 'libdbusmenu-gtk3' 'libindicator3')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-application_${_actual_ver}${_extra_ver}.orig.tar.gz"
        'indicator-application.service'
        '0001_autoconf.patch')
sha512sums=('367b55807210125ec83d548ef30feeb2b191f321e1bec6e0c2956b810e07dc3185f7591204c7af0ef6db05d5eff4b456d74591ae439128ff010ece05ab812c15'
            '8431d7c765b6b34addf9d6d3700ce42454b8557454e74af9d7bb9fe2b7e7bf8f98a9e66707c1876c460c1f7e2dc23fe057d6583ac90a6ad917ec8105fd405aca'
            '7ef9f2eab8dd17d8957b706ba479c38f5f9e74a69e77af7c8e318809c67b4aea214433f0364741fc4fcbfaefd6bbff3e370e24048cd23f2e09ebe6bca32b1511')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  autoreconf -vfi
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m644 "${srcdir}/indicator-application.service" \
                "${pkgdir}/usr/share/dbus-1/services/"
}

# vim:set ts=2 sw=2 et:
