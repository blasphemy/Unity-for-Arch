# Maintainer: Xiao-Long Chen
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libdbusmenu
pkgname=('libdbusmenu' 'libdbusmenu-gtk2' 'libdbusmenu-gtk3')
# Ubuntu: please stop changing your versioning scheme
_realver=12.10.3+13.10.20130628
pkgver=12.10.3daily13.06.28
pkgrel=100
pkgdesc="A small little library that was created by pulling out some comon code out of indicator-applet"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('LGPL')
makedepends=('json-glib' 'gtk2' 'gtk3' 'intltool' 'gnome-doc-utils' 'gtk-doc' 'gobject-introspection' 'vala' 'python2' 'valgrind')
checkdepends=('dbus-test-runner' 'xorg-server-xvfb')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libdbusmenu_${_realver}.orig.tar.gz"
        '0001_autoconf.patch')
sha512sums=('2157e2bb7df160ee74ec721fe94af62076f319abbf4e5a2b144323483f8a7895b033d781c571e6aa88c66a3f2e41d6d13e9e77e7945a55e171bdfc8f2282bb0b'
            '233865822a6280dbd536db131511648bdf40af042ae8afa1ed813cd2538c4e9f5f1ada8868de27bf60f1bed8830ace9916c097ba3d8d698744a2fac95a6dbabf')

prepare() {
  cd "${srcdir}/${pkgbase}-${_realver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
}

build() {
  cd "${srcdir}/${pkgbase}-${_realver}"

  gtkdocize
  autoreconf -vfi
  intltoolize -f

  # Python2 fix
  sed -i "s|^\(#!.*python\)$|\12|" tools/dbusmenu-bench

  #" # Stupid vim highlighting

  # libdbusmenu-gtk2 won't install if libdbusmenu-glib is missing
  sed -i 's|$(DBUSMENUGTK_CFLAGS)|$(DBUSMENUGTK_CFLAGS) -L../libdbusmenu-glib/.libs|' libdbusmenu-gtk/Makefile.{am,in}

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-scrollkeeper --with-gtk=2 --disable-static
  export MAKEFLAGS="-j1"
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-scrollkeeper --with-gtk=3 --disable-static
  make
  popd
}

check() {
  cd "${srcdir}/${pkgbase}-${_realver}/build-gtk3"

  make check || :
}

# I've modified thn81's PKGBUILD so that the directory structure of the packages
#   are identical to Ubuntu's. It should have better compatibility will other
#   packages that depend on libdbusmenu.

package_libdbusmenu() {
  depends=('json-glib')

  cd "${srcdir}/${pkgbase}-${_realver}/build-gtk3"

  export MAKEFLAGS="-j1"
  make -C libdbusmenu-glib DESTDIR="${pkgdir}/" install
  make -C tools DESTDIR="${pkgdir}/" install
  make -C tests DESTDIR="${pkgdir}/" install
  make -C docs/libdbusmenu-glib DESTDIR="${pkgdir}/" install
  make -C po DESTDIR="${pkgdir}/" install
}

package_libdbusmenu-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2' "libdbusmenu=${pkgver}")

  cd "${srcdir}/${pkgbase}-${_realver}/build-gtk2"

  export MAKEFLAGS="-j1"
  make -C libdbusmenu-gtk DESTDIR="${pkgdir}/" install
  make -C docs/libdbusmenu-gtk DESTDIR="${pkgdir}/" install
}

package_libdbusmenu-gtk3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3' "libdbusmenu=${pkgver}")

  cd "${srcdir}/${pkgbase}-${_realver}/build-gtk3"

  export MAKEFLAGS="-j1"
  make -C libdbusmenu-gtk DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
