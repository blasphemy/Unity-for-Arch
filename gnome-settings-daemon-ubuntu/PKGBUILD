# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-settings-daemon-ubuntu
_ppa_rel=0ubuntu1~saucy1
_translations=20130418
pkgver=3.10.2
pkgrel=100
pkgdesc="The GNOME Settings daemon with Ubuntu's patches"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('dconf' 'gnome-desktop' 'gsettings-desktop-schemas-ubuntu' 'hicolor-icon-theme' 'ibus' 'libcanberra-pulse' 'libnotify' 'libsystemd' 'libwacom' 'pulseaudio' 'pulseaudio-alsa' 'upower' 'librsvg' 'libgweather' 'geocode-glib' 'geoclue2' 'libappindicator3' 'packagekit')
makedepends=('intltool' 'xf86-input-wacom' 'libxslt' 'docbook-xsl' 'desktop-file-utils')
groups=('unity')
provides=("gnome-settings-daemon=${pkgver}")
conflicts=('gnome-settings-daemon')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-settings-daemon/gnome-settings-daemon_${pkgver}-${_ppa_rel}.debian.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-settings-daemon.tar.gz"
        '0001_Do_not_hide_IBus_icon.patch'
        '0002_Do_not_hide_cursor.patch')
sha512sums=('4e937bf178086c3582a7072ce6f2db60b84dac1171ff8344b7b90876b415f0cef0924403308f86cd73f08d6490ae011d533447356c59086e16f41732f1860a87'
            '72459c13638ec9f712ed03235effe253b9878c7aeaa84676cfb24b94a133551f83da79aefe2b877dddad73ec863890d84199910ed308740aaf50abafe3ea301a'
            '8282c8c9233b3dd2199ae3bcedd910ff68a69896ae295300fb59c3d03119ac5e889a2088c2021c2fc2afd452d167b3ee22c92ad9c6375a1588f1f8dff6b172ff'
            '2a5a4fdc030ad7499c4551ba33480a7c4b609f018c9bebcb21f49deecf54a73faf1f6078b93895fdefb5738ee8a4aad9fe4ae7e95243242d6d82ecbeb5b9754f'
            '20cda1af538d610568c5612c84a46cc26fab33c565910ce15542197c37a6b85c92dcbe3ce7f32c404328d3c6af402e28a9e164d7ddabcdf977a0f0ea3071b918')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Do not hide ibus icon. This will cause a duplicate icon to show up in GNOME
  # Shell, but whatever.
  patch -p1 -i "${srcdir}/0001_Do_not_hide_IBus_icon.patch"

  # The cursor is hidden by default in GNOME 3.10. Unfortunately, LightDM and
  # Unity do not enable it again, so we won't hide it initially.
  patch -p1 -i "${srcdir}/0002_Do_not_hide_cursor.patch"

  # Apply Ubuntu's patches
    # Disable patches
      # Ubuntu specific
        sed -i '/43_disable_locale_settings.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/53_sync_input_sources_to_accountsservice.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying patch ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname%-*}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnome-settings-daemon \
    --disable-static \
    --enable-systemd
      
  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Part of gnome-settings-daemon-compat
  rm "${pkgdir}/usr/lib/gnome-settings-daemon/gnome-fallback-mount-helper"
}

# vim:set ts=2 sw=2 et:
