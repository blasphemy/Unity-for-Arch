# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=dee-ubuntu
_actual_ver=1.2.5
_extra_ver=ubuntu1daily13.06.14
pkgver=${_actual_ver}${_extra_ver}
pkgrel=1
pkgdesc="Model to synchronize multiple instances over DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
groups=('unity')
depends=('dbus-glib' 'glib2' 'icu' 'python' 'python-gobject' 'python2' 'python2-gobject')
makedepends=('gnome-common' 'gobject-introspection' 'gtk-doc' 'vala')
checkdepends=('dbus-test-runner')
provides=("dee=${pkgver}")
conflicts=('dee')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/dee_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('51506e2cb22f896c4f8d5c4ffc5bce671b5730b25b2fd420f45aa403d49f9fb3473fafcf9f9847be9310547f1e9d0024d026774e4864ebc7909ec6be99b1e9d8')

build() {
  cd "${srcdir}/dee-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  mkdir build-python3 && cd build-python3
  PYTHON=python3 ../configure --prefix=/usr --enable-gtk-doc
  make
  cd ..

  mkdir build-python2 && cd build-python2
  PYTHON=python2 ../configure --prefix=/usr
}

check() {
  cd "${srcdir}/dee-${_actual_ver}${_extra_ver}/build-python3"
  make check || true
}

package() {
  cd "${srcdir}/dee-${_actual_ver}${_extra_ver}"

  cd build-python3/
  make DESTDIR="${pkgdir}/" install
  cd ..

  cd build-python2/bindings/python/
  make DESTDIR="${pkgdir}/" install
}
