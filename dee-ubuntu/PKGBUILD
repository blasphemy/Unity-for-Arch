# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=dee-ubuntu
_actual_ver=1.2.5ubuntu1
_extra_ver=+13.10.20130630
pkgver=${_actual_ver}daily13.06.30
pkgrel=1
pkgdesc="Model to synchronize multiple instances over DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
groups=('unity')
depends=('dbus-glib' 'glib2' 'icu' 'python' 'python-gobject' 'python2' 'python2-gobject')
makedepends=('gnome-common' 'gobject-introspection' 'gtk-doc' 'vala')
checkdepends=('dbus-test-runner')
provides=("dee=${pkgver}")
conflicts=('dee')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/dee_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('b7b4a604e08fcecc6d8e170405938947e5088777da4bb7f7fd31f7af9d1c11f84a654fb73ad8b3218582e848dd623a85a4a58935f9630a0e1f4ea0111382ab95')

build() {
  cd "${srcdir}/dee-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  mkdir build-python3 && cd build-python3
  PYTHON=python3 ../configure --prefix=/usr --enable-gtk-doc
  make
  cd ..

  mkdir build-python2 && cd build-python2
  PYTHON=python2 ../configure --prefix=/usr
}

check() {
  cd "${srcdir}/dee-${_actual_ver}${_extra_ver}/build-python3"
  make check || true
}

package() {
  cd "${srcdir}/dee-${_actual_ver}${_extra_ver}"

  cd build-python3/
  make DESTDIR="${pkgdir}/" install
  cd ..

  cd build-python2/bindings/python/
  make DESTDIR="${pkgdir}/" install
}
