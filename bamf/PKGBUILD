# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=bamf
_actual_ver=0.5.1
_extra_ver=+13.10.20130830
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=101
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('libgtop' 'libunity-webapps' 'libwnck3' 'glib2')
makedepends=('gnome-common' 'gobject-introspection' 'gtk-doc' 'libxml2' 'libxslt' 'vala')
provides=('FEATURE-bamf-webapps' "libbamf3=${pkgver}" "bamfdaemon=${pkgver}")
replaces=('libbamf3' 'bamfdaemon')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('7ebe2d64b93e9a4eeff7638c56b469d25aa3819b4ce641e17310561989bc5829c3cc9613c288fd9b186257e0214f92ab75e849ffbb4691d1fcf958d07331a0ef')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  sed -i 's/-Werror/-Wno-error/g' configure.ac
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  gtkdocize
  autoreconf -vfi

  export PYTHON=/usr/bin/python2
  
  if ! pacman -Q xorg-server-xvfb &>/dev/null; then
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  else
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc --enable-headless-tests
  fi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}
