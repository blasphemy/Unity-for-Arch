# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=ido
_actual_ver=13.10.0
_extra_ver=+13.10.20131031
pkgver=${_actual_ver}daily13.10.31
pkgrel=100
pkgdesc="Widgets and other objects used for indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/ido"
license=('LGPL')
depends=('gtk3-ubuntu' 'glib2')
makedepends=('gobject-introspection' 'gtk-doc' 'vala' 'xorg-util-macros')
groups=('unity')
options=('!libtool')
provides=('ido3')
conflicts=('ido3')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/ido_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('ebc0e2ec48374103ba615e0c61b6c2802ba3df4df72446d887cd24d15a0728b0179db0682ddf99af6d51d9a3016bc2168ad3fce231b41077119d2d71b58a3123')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --disable-introspection
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
