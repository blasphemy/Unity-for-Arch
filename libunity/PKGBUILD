# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libunity
_actual_ver=7.1.2
_extra_ver=+13.10.20131003
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=101
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee06' 'dee-ubuntu' 'libdbusmenu' 'gtk3')
makedepends=('intltool' 'vala' 'python2' 'gobject-introspection' 'vala0.18')
install=${pkgname}.install
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        '0001_autoconf.patch'
        '0002_privacy_by_default.patch')
sha512sums=('bf9956399319d628f552bb51af611e85b1d8d328cda714c1990bdd62aef5a12e29a93aba80d70a127e2e2bc74e9d3b6a14a39d292767a2fe62ee679772953560'
            '0723db0e0d46fb17b8636103f2d8a6f72839d787a2b67aa158586d09ff81fd125c2fbc562a5ec498bb894a19e3e6fc17ae9e696afb4b6e4fb9dc5896626b6561'
            '1570c01f4c8ac53bc7806fad78691844a12625b7142270b5a215d203618acc76ba258c4aa969a120186ba9224b726f20a2a9efad05940ffdaa258be49844fa4b'
            '83769f9f54abb8090d5307affba8f974a23ee11dac74d9c3da389b48c9bd80fac241731017c9dcb05af14d4c644228d67af558e97972fceb144a8fc0deed30f7')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  find -type f -name '*.py' -exec sed -i 's|^\(#!.*python$\)|\12|g' {} \+

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
  patch -p1 -i "${srcdir}/0002_privacy_by_default.patch"

  # Apply Ubuntu patches
  patch -p1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"

  for i in $(grep -v '#' 'debian/patches/series'); do
    msg "Applying ${i} ..."
    patch -p1 -i "debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  #export VALAC=valac-0.18
  mkdir bin/
  ln -s /usr/bin/valac-0.18 ./bin/valac
  export PATH="$(pwd)/bin:${PATH}"

  intltoolize -f
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --enable-headless-tests PYTHON=python2
  export MAKEFLAGS="-j1"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
