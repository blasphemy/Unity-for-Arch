# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libunity
_actual_ver=7.0.2
_extra_ver=daily13.06.06.1
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver}
pkgrel=103
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee06' 'dee-ubuntu' 'libdbusmenu' 'gtk3')
makedepends=('intltool' 'vala' 'python2' 'gobject-introspection' 'vala0.18')
checkdepends=('python2-gobject' 'xorg-server-xvfb' 'xorg-xauth')
install=$pkgname.install
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        '0001_autoconf.patch')
sha512sums=('d618c8f0a24adee008042359793e2c107397b819f784e58625027d504536e90c9b66bf9aaa54a1c8a6fb0d26b8a350aa0864b09fb47078ec82663b9e9b817c3c'
            '749c41f824edc7b73bf0c844eb4472425f442140623e60887f46068f54ac5619951e89dfe29dcf3e72f5aa1fe825b579561900611badc3fe98a4ec545b060652'
            '1570c01f4c8ac53bc7806fad78691844a12625b7142270b5a215d203618acc76ba258c4aa969a120186ba9224b726f20a2a9efad05940ffdaa258be49844fa4b')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  find -type f -name '*.py' -exec sed -i 's|^\(#!.*python$\)|\12|g' {} \+

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  # Apply Ubuntu patches
  patch -p1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"

  for i in $(grep -v '#' 'debian/patches/series'); do
    msg "Applying ${i} ..."
    patch -p1 -i "debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  #export VALAC=valac-0.18
  mkdir bin/
  ln -s /usr/bin/valac-0.18 ./bin/valac
  export PATH="$(pwd)/bin:${PATH}"

  intltoolize -f
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --enable-headless-tests PYTHON=python2
  export MAKEFLAGS="-j1"
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  #export XDG_DATA_DIRS="$(pwd)/test/data:${XDG_DATA_DIRS}"

  # test-appinfo-manager is failing: something really bad is probably going to
  # happen
  sed -i -e '/\/Unit\/AppInfoManager\/SyncLookupOk/d' \
         -e '/AppInfoManagerSuite\.test_sync_lookup_ok/d' \
         -e '/\/Unit\/AppInfoManager\/AsyncLookupOk/d' \
         -e '/AppInfoManagerSuite\.test_async_lookup_ok/d' \
         test/vala/test-appinfo-manager.vala

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
