# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libunity
_actual_ver=7.0.9
_extra_ver=+13.10.20130723
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=100
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee06' 'dee-ubuntu' 'libdbusmenu' 'gtk3')
makedepends=('intltool' 'vala' 'python2' 'gobject-introspection' 'vala0.18')
checkdepends=('python2-gobject' 'xorg-server-xvfb' 'xorg-xauth')
install=${pkgname}.install
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        '0001_autoconf.patch'
        '0002_privacy_by_default.patch')
sha512sums=('4863ec737998b5f1f0cd47b59696a6093f6990238fcfec11d208fd919785c7fda4b2a3f8fc8ceb65b7414bb57875a9d82706bc1b99985200a85b71bba1773c7c'
            '1d2a436d23478d611954f40fba0f1d56abc6dce65628008f94b781d4ab1c17c43a3954f0a7e1618a943e73f1bc2f8dceeb51db2dff38937ce2c4e44a3cb71647'
            '1570c01f4c8ac53bc7806fad78691844a12625b7142270b5a215d203618acc76ba258c4aa969a120186ba9224b726f20a2a9efad05940ffdaa258be49844fa4b'
            '83769f9f54abb8090d5307affba8f974a23ee11dac74d9c3da389b48c9bd80fac241731017c9dcb05af14d4c644228d67af558e97972fceb144a8fc0deed30f7')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  find -type f -name '*.py' -exec sed -i 's|^\(#!.*python$\)|\12|g' {} \+

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
  patch -p1 -i "${srcdir}/0002_privacy_by_default.patch"

  # Apply Ubuntu patches
  patch -p1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"

  for i in $(grep -v '#' 'debian/patches/series'); do
    msg "Applying ${i} ..."
    patch -p1 -i "debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  #export VALAC=valac-0.18
  mkdir bin/
  ln -s /usr/bin/valac-0.18 ./bin/valac
  export PATH="$(pwd)/bin:${PATH}"

  intltoolize -f
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --enable-headless-tests PYTHON=python2
  export MAKEFLAGS="-j1"
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  #export XDG_DATA_DIRS="$(pwd)/test/data:${XDG_DATA_DIRS}"

  # test-appinfo-manager is failing: something really bad is probably going to
  # happen
  sed -i -e '/\/Unit\/AppInfoManager\/SyncLookupOk/d' \
         -e '/AppInfoManagerSuite\.test_sync_lookup_ok/d' \
         -e '/\/Unit\/AppInfoManager\/AsyncLookupOk/d' \
         -e '/AppInfoManagerSuite\.test_async_lookup_ok/d' \
         test/vala/test-appinfo-manager.vala

  make check || :
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
