# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-lens-video
_actual_ver=0.3.15
_extra_ver=+13.10.20130705
_translations=20130418
pkgver=${_actual_ver}.daily13.07.05
pkgrel=100
pkgdesc="Unity Video lens"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-videos"
license=('GPL')
depends=('dee' 'json-glib' 'libgee06' 'libsoup-gnome' 'libunity' 'libzeitgeist' 'unity-lens-music')
makedepends=('gnome-common' 'intltool' 'vala')
checkdepends=('xorg-server-xvfb')
provides=("unity-scope-video-remote=${pkgver}")
conflicts=('unity-scope-video-remote')
replaces=('unity-scope-video-remote')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-video_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-unity-lens-video.tar.gz")
sha512sums=('cd2ebe9c17201b72ac00dbee81b2ff3afc2b7e8280059a5cac626d4614890a6cc70030c0aabb2277f0526a75aa151076f80468465169820c00d31afa3ed6ad8e'
            'cac380942dd525e405dcac5c348e0aac8c734cb5ee2d2ef034a1a4a0acbeb54dc5598dc40ab7e4b0fcbe28eec322663b15a1df09fc36aec091c1f959ec67b700')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib --enable-headless-tests
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
