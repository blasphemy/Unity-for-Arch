# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=nux
_actual_ver=4.0.2
_extra_ver=+13.10.20130723
pkgver=${_actual_ver}.daily13.07.23
pkgrel=100
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('LGPL')
depends=('gdk-pixbuf2' 'glew' 'pango' 'mesa' 'libsigc++' 'libxcomposite' 'libxdamage' 'libxxf86vm' 'geis' 'ibus')
makedepends=('boost' 'glproto' 'dri2proto' 'doxygen' 'graphviz')
checkdepends=('gmock-ubuntu')

groups=('unity')
options=('!libtool' '!emptydirs')

install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/nux_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autoconf.patch'
        '01_blacklist_llvmpipe.patch')
sha512sums=('75ad25b29f2723b5ecc36c4b7e3063a1f04830bf38aa254108bc5542f752b4c399afa667ef8fe60cadd56ab78d8e8548cc0ea921d12097821c81bb7596db9e79'
            'e0a3d3d9cf7d4ba7da541a6383e5c545b37ce4099c1e7b716b18c47e68f0068d522ab06658f759df9c717503a46f0ca946ef2f256264c5c3fbb03fc7635038e2'
            'a6122d47bef574df7e4f3aa3ddff34ca7a6e291878665f3dfdbb9ea071394180ab31aadcb79efd53ebb1ec2a86a15d53c9394478305c3fd5f10ece049265b374')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
  patch -p1 -i "${srcdir}/01_blacklist_llvmpipe.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static

  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make check-headless || :
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}" install

  rm -rv "${pkgdir}/usr/share/nux/gputests/"
}

# vim:set ts=2 sw=2 et:
