# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gnome-control-center-unity
_translations=20130418
pkgver=1.3daily13.05.20ubuntu.unity.next
pkgrel=1
pkgdesc="A set of settings panels for configuring the Unity desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/gnome-control-center-unity"
license=('GPL')
groups=('unity')
depends=('gnome-control-center-ubuntu' 'gsettings-desktop-schemas' 'libnotify' 'libxml2')
makedepends=('gnome-common' 'intltool')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/gnome-control-center-unity_${pkgver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-control-center-unity.tar.gz")
sha512sums=('e93358f8d7a6356bdecef930fa70f51c4a90bdc159c8779196198bbf82f84deb5790d7d33e08fa3e061de384d20b51312d992b9678f6c0be8780808c587e3b9a'
            'e9dd7b099df99c55938cf882639e8cfc03101e97761f3209db7ad5b218f5f6345cd49e5679fe7033264f1676dea03a093351ecf14b5ea8d172390a08bfa7759d')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po "${srcdir}"/gnome-control-center-unity/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # GNOME breaking stuff for no reason once again
  cd "${pkgdir}/usr/share/applications/"
  mv gnome-control-center-unity-appearance.desktop \
     gnome-unity-appearance-panel.desktop
}

# vim:set ts=2 sw=2 et:
