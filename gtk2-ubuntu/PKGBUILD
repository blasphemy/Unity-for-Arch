# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>

pkgname=gtk2-ubuntu
_ubuntu_rel=0ubuntu2
pkgver=2.24.18
pkgrel=101
pkgdesc="GTK+ is a multi-platform toolkit (v2)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'libcups' 'gtk-update-icon-cache' 'python2')
makedepends=('pkg-config' 'gobject-introspection' 'gtk-doc')
provides=("gtk2=${pkgver}")
conflicts=('gtk2')
replaces=('gtk2-docs')
backup=('etc/gtk-2.0/gtkrc')
options=('!libtool')
install=arch_gtk2.install
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtk+2.0_${pkgver}-${_ubuntu_rel}.debian.tar.gz"
        'arch_xid-collision-debug.patch'
        'fix-ubuntumenuproxy-build.patch')
sha512sums=('5250ae00c8d5fcefded54c8476e45f6bcc69d1207457ed53a23cacf3986cbbbcd115c2b5b823c1f90f1e21de4eafb40c7081a7175943ce2adda593bf2759d0da'
            '087355220c241f3a7edcab84191da19690f48df836a63d62cb6cc2c42228d591072c41075dc2d51469c2012d18abc90f25352e7c77de09eefb465c88b21e5db5'
            '89e3223c86731a76b12d39245f1ec4cf8b4860ab1d11d74a10e5deb0db302f20b4bb5d9034da1d8aeb96369dbdeddcdd2f28943270dc501476c694562b960159'
            '4b27afecb82e3517c2584665f8d2a2911193254a00b82ff8064caf4016ab029c76e99af502bb9271a66209594b50b2729797defcc9512d341aa61aff4f64ec24')

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"

  patch -p1 -i "${srcdir}/arch_xid-collision-debug.patch"

  # Apply Ubuntu patches

  # Certain patches should not be used
    # Multiarch
      sed -i '/041_ia32-libs.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/098_multiarch_module_path.patch/d' "${srcdir}/debian/patches/series"
    # Static linking stuff for udebs
      sed -i '/001_static-linking-dont-query-immodules.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/002_static-linking-dont-build-perf.patch/d' "${srcdir}/debian/patches/series"
    # Breaks pkgconfig
      sed -i '/003_gdk.pc_privates.patch/d' "${srcdir}/debian/patches/series"
    # gtk.immodules is in /etc/gtk-2.0/ in Arch Linux, not
    # /usr/lib/gtk-2.0/2.10.0/
      sed -i '/011_immodule-cache-dir.patch/d' "${srcdir}/debian/patches/series"

  # Build fix from György Balló
  patch -p1 -i "${srcdir}/fix-ubuntumenuproxy-build.patch"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/gtk+-${pkgver}"

  autoreconf -vfi

  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-xinput=yes \
    --enable-test-print-backend

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"

  make DESTDIR="${pkgdir}" install
  sed -i "s#env python#env python2#" "${pkgdir}/usr/bin/gtk-builder-convert"
  echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
  # Use the official gtk-update-icon-cache package
  rm -v "${pkgdir}/usr/bin/gtk-update-icon-cache"
}

# vim:set ts=2 sw=2 et:
