# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gmock-ubuntu
_actual_ver=1.6.0
_extra_ver=+svn437
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="Google's framework for writing and using C++ mock classes"
arch=('i686' 'x86_64')
url="http://code.google.com/p/googlemock/"
license=('BSD')
groups=('unity-tests')
depends=('gtest-ubuntu' 'python2')
makedepends=('cmake')
provides=("gmock=${pkgver}")
conflicts=('gmock')
options=('!libtool' '!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/google-mock_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/google-mock_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('82f15d07e2068b3c50ae581f36bb5c43cb94cd415240352ec4710c92488eec9e55d730e130e7f850fad5f44419239dc3f26bb48771dae7976389233af69f5755'
            '4f973b0ab7457af654a735ecac8da179f7b32beda7703819f090c3d6ab657568be05199fc52a24b700b7195550fdb153053990f6b5ac6eae0cf0731f3e949729')

build() {
  cd "${srcdir}/google-mock"

  find -type f -exec chmod u+w {} \+
  find -type f -name '*.py' -exec sed -i 's|^\(#!.*python$\)|\12|g' {} \+

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi
  ./configure --enable-static=no --prefix=/usr
  #make
}

check() {
  cd "${srcdir}/google-mock"
  #make check
}

package() {
  cd "${srcdir}/google-mock"

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 scripts/generator/gmock_gen.py "${pkgdir}/usr/bin/"

  install -dm755 "${pkgdir}/usr/share/gmock/cpp/"
  install -m644 scripts/generator/cpp/* "${pkgdir}/usr/share/gmock/cpp/"

  install -dm755 "${pkgdir}/usr/include/"
  cp -a include/gmock/ "${pkgdir}/usr/include/"

  install -dm755 "${pkgdir}/usr/src/gmock/src/"
  install -m644 src/*.cc "${pkgdir}/usr/src/gmock/src/"

  install -m644 CMakeLists.txt "${pkgdir}/usr/src/gmock/"

  cp -a gtest/ "${pkgdir}/usr/src/gmock/"

  install -dm755 "${pkgdir}/usr/share/man/man1/"
  install -m644 "${srcdir}/debian/gmock_gen.1" "${pkgdir}/usr/share/man/man1/"

  for i in "${pkgdir}"/usr/bin/*.py; do mv ${i} ${i%.py}; done
}

# vim:set ts=2 sw=2 et:
