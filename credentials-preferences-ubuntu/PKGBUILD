# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=credentials-preferences-ubuntu
_translations=20130420
_actual_ver=0.1.7
_extra_ver=~+13.10.20130724.1
pkgver=${_actual_ver}${_extra_ver/\~+/.}
pkgrel=1
pkgdesc="Control panel for configuring online account credentials and settings"
arch=('i686' 'x86_64')
url="https://launchpad.net/gnome-control-center-signon"
license=('GPL' 'LGPL')
provides=('credentials-preferences')
conflicts=('credentials-preferences')
depends=('libaccounts-glib' 'libsignon-glib' 'gtk3' 'gnome-control-center-ubuntu')
makedepends=('gobject-introspection' 'vala' 'intltool' 'itstool' 'gtk-doc' 'yelp-tools')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/gnome-control-center-signon_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-credentials-preferences.tar.gz")
sha512sums=('92e81852f11b24909863ce9202ebf4516f11d59e4629090fbd3c1908d8a5a2a1fb4c635f7dfc41ce4b6505d6c086b1b9c8441ef90b7f266f19d2ecaa8b2d2b52'
            '6684ab6452f793f474b8b1641182ba6254119cb82306cb4a7c296c6e080b4f451c0362fa0c90599c74ded5246e0f649fd3cc8839b976102a8ceba4fe08e657bf')

prepare() {
  cd "${srcdir}/gnome-control-center-signon-${_actual_ver}${_extra_ver}"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/credentials-control-center-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/gnome-control-center-signon-${_actual_ver}${_extra_ver}"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/credentials-preferences \
    --disable-static

  make
}

package() {
  cd "${srcdir}/gnome-control-center-signon-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
