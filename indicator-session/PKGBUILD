# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-session
_actual_ver=12.10.5
_extra_ver=+14.04.20131125
_translations=20130419
pkgver=${_actual_ver}daily13.11.25
pkgrel=101
pkgdesc="Indicator showing session management, status and user switching"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
depends=('dconf')
makedepends=('cmake' 'intltool' 'python2')
optdepends=('lightdm: support for lightdm')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-session_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-session.tar.gz"
        'indicator-session.service'
        '0001_Lock_Screen_Live_CD_Detection.patch'
        '0002_There_is_no_help.patch')
sha512sums=('3e70f857487cbd01e3bf2384ad03a4f66fa0a77abf268947af07f05b6592c0dc284f1a2229e2b12b535482d5eb5e4d195d08196263cf23684b5f4eb2e2980569'
            'd58470441812a2500bd709877f8b3dcb629e095e8612c413cc34ee8f6101f0c15b25605b1deba3741c8e241711c9bb1bb2169e0acae923f9750afee4717c183f'
            '540c76c0f68b028194bb1000dd61a0c24f2f16427e79d237b94b8c65fe5e3e16eb6ea645f0fd33d734113727e25b6eb7452cb72f72399c9e8d370f74abba7511'
            '5528add26d0d81dade7d9ae2868b43b09425201bb98de8e0dddec7ac84dc5ac93ad777e89b092e9c8f7363f4c87144abd340f6a74cb00e3f6d44fbd1ea522a12'
            '5326ff4022fe86fa3162d1f46341f2f352d4859bd5024da81d5640465cd3b242abedf820c2589c1da05e2eec3fc2e7535a2e4f2f8489333cb001e279094a4a15')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Lock_Screen_Live_CD_Detection.patch"
  patch -p1 -i "${srcdir}/0002_There_is_no_help.patch"

  msg "Merging translations from ${_translations}"
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -Denable_tests=OFF \
    -Denable_lcov=OFF

  make
}

package() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build"

  make DESTDIR="${pkgdir}/" install

  rm -vf "${pkgdir}/test-service"

  # Please fix CMake scripts, Ubuntu...
  rm -rv "${pkgdir}/usr/share/locale/LC_MESSAGES/"

  for i in ../po/*.po; do
    FILELANG=$(basename ${i})
    FILELANG=${FILELANG%.po}
    install -dm755 "${pkgdir}/usr/share/locale/${FILELANG}/LC_MESSAGES/"
    msgfmt -v ${i} -o \
      "${pkgdir}/usr/share/locale/${FILELANG}/LC_MESSAGES/indicator-session.mo"
  done

  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m644 "${srcdir}/indicator-session.service" \
                "${pkgdir}/usr/share/dbus-1/services/"
}

# vim:set ts=2 sw=2 et:
