# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-gtk-module
_actual_ver=0.0.0
_extra_ver=+14.04.20131125
pkgver=${_actual_ver}daily13.11.25
pkgrel=1
pkgdesc="Unity GTK+ Module"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-gtk-module"
license=('GPL')
groups=('unity')
depends=('libx11' 'gtk2-ubuntu' 'gtk3')
makedepends=('gtk-doc')
provides=('appmenu-gtk2' 'appmenu-gtk3')
replaces=('appmenu-gtk2' 'appmenu-gtk3')
conflicts=('appmenu-gtk2' 'appmenu-gtk3')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-gtk-module_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '80unity-gtk-module')
sha512sums=('a45bbabd5d37b50c0628b2224fd8b956a2c19e215e4a04ff975b7f7b08499e5c3318bf1c3caa4f6c5ebbb58d53410d4b27bfd58e88d7c5801dd04c986687cfd9'
            'cbf13533c128c870dc4b9bb3436a95c3a38ec3803cf99f1221416c0d78b971a66b0b279d765aab8a6942148a89638c569031050012bfb5c53bb7d5daae14189c')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  [ -d build-gtk2 ] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --enable-gtk-doc \
               --disable-static
  make
  popd

  [ -d build-gtk3 ] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --enable-gtk-doc \
               --disable-static
  make
  popd
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  pushd build-gtk2
  make DESTDIR="${pkgdir}/" install
  popd

  pushd build-gtk3
  make DESTDIR="${pkgdir}/" install
  popd

  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755  "${srcdir}/80unity-gtk-module" \
                 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  rm -rvf "${pkgdir}/usr/share/upstart/"
}
