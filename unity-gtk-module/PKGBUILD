# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-gtk-module
_actual_ver=0.0.0
_extra_ver=+13.10.20130716.1
pkgver=${_actual_ver}daily13.07.16.1
pkgrel=1
pkgdesc="Unity GTK+ Module"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-gtk-module"
license=('GPL')
groups=('unity')
depends=('libx11' 'gtk2-ubuntu' 'gtk3')
makedepends=('gtk-doc')
provides=('appmenu-gtk2' 'appmenu-gtk3')
replaces=('appmenu-gtk2' 'appmenu-gtk3')
conflicts=('appmenu-gtk2' 'appmenu-gtk3')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-gtk-module_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '80unity-gtk-module')
sha512sums=('220701431c12a5c45a65af153d5eaf424ee8bbf905743fe8bd61c6c5817383de000076667b9fd0b03b9564ab8da8a06977945d92726f7596e5b971f91b8c8de8'
            'e852cbb1a10a3b761e062e5b486b6075bcad85b9b1cf55651d6a788dd4a0ceb088065232b5a780bec47b4cbec6162f2d15c2d68566af7278123f49fced3b96ff')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  [ -d build-gtk2 ] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --enable-gtk-doc \
               --disable-static
  make
  popd

  [ -d build-gtk3 ] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --enable-gtk-doc \
               --disable-static
  make
  popd
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  pushd build-gtk2
  make check
  popd

  pushd build-gtk3
  make check
  popd
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  pushd build-gtk2
  make DESTDIR="${pkgdir}/" install
  popd

  pushd build-gtk3
  make DESTDIR="${pkgdir}/" install
  popd

  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755  "${srcdir}/80unity-gtk-module" \
                 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  rm -rvf "${pkgdir}/usr/share/upstart/"
}
