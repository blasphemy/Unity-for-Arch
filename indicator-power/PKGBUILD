# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=indicator-power
_actual_ver=12.10.6
_extra_ver=+13.10.20131004
_translations=20130419
pkgver=${_actual_ver}daily13.10.04
pkgrel=101
pkgdesc="An indicator to show the power status of your devices"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-power"
license=('GPL')
depends=('dconf')
makedepends=('intltool' 'python2')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-power_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-power.tar.gz"
        '0001_Not_a_phone.patch')
sha512sums=('d1e4e41d11340e5b8b2a20a56ec92be0d03b927a8c628cc624a3bdfdb33b67a7d1772dfad516b7a17eba394a9ced84c05d330b2c85919f1fab9d210c89d74ab0'
            'd597ec5e3cac6bfb95b96fb73d88b052a2e99bd56b1f259b3b6452da42b40ac3ad14b4fd579e8f940189834ba49b95c9d5acd58dc32edf7a712e496786370671'
            'd06da9469b33681d7468f31aef78fee4631abe381d4b9a4c7735d8158a8851edc77c7a4599198d8b2e877d734ae931dfa64defc5a42d7776092e4260d37704e7')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Not_a_phone.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  autoreconf -vfi
  intltoolize -f
  # Static libraries are needed for the tests
  ./configure --prefix=/usr --libexecdir=/usr/share/${pkgname} #--disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  find "${pkgdir}" -type f -name '*.a' -delete
}

# vim:set ts=2 sw=2 et:
