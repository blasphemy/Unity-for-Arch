# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Christopher Reimer <github@creimer.net>

pkgname=gtk3-ubuntu
_ubuntu_rel=0ubuntu1
pkgver=3.10.6
pkgrel=101
pkgdesc="GObject-based multi-platform toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=arch_gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord' 'at-spi2-atk' 'wayland' 'libxkbcommon')
makedepends=('gobject-introspection')
optdepends=('gnome-themes-standard: Default widget theme'
            'gnome-icon-theme: Default icon theme')
provides=("gtk3=${pkgver}")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=('etc/gtk-3.0/settings.ini')
license=('LGPL')
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_${pkgver}-${_ubuntu_rel}.debian.tar.gz"
        'arch_settings.ini')
sha512sums=('2619d2e08dbdde41528ad132f120c91b7536d01a6932a9dc9ac5c5b2ba7e68bcebbd91374189160519da20794957b5dcd05502f92c2acecb01bfc99444f0617b'
            '8667f766fd6451c54040da9db920a04585546b341606911bbe2691c4fe9f430d712d65ef0bc0952b115a4a941731d0c871e16ffc98859dc00aee80d371525aa8'
            '1642d77622d61234e316e8fcbc803a6a5556c606e37e56aa5981ef2f2df85bfa959c31b5d1bff248b340760e1178281cb0d7abdf540c5f7d4b62cb383a67c685')

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"

  # Apply Ubuntu Patches
    echo > "${srcdir}/debian/patches/series"
    # Enable patches
      # https://bugs.launchpad.net/indicator-messages/+bug/1088162
      # Dependency check: grep -R ubuntu-private.h * | cut -d/ -f1
        echo 'ubuntu_gtk_custom_menu_items.patch' >> "${srcdir}/debian/patches/series"
      # Do not allow offscreen widgets to grab the cursor
        echo '016_no_offscreen_widgets_grabbing.patch' >> "${srcdir}/debian/patches/series"
        echo '017_no_offscreen_device_grabbing.patch' >> "${srcdir}/debian/patches/series"
      # Allow printing to printers advertised using Avahi/Bonjour when CUPS 1.6
        echo 'print-dialog-show-options-of-remote-dnssd-printers.patch' >> "${srcdir}/debian/patches/series"
      # Other
        echo 'bzg_gtkcellrenderer_grabbing_modifier.patch' >> "${srcdir}/debian/patches/series"
        echo 'git_menu_separator_style.patch' >> "${srcdir}/debian/patches/series"
        echo 'uimanager-guard-against-nested-node-updates.patch' >> "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/gtk+-${pkgver}"

  autoreconf -vfi

  #CXX=/bin/false ./configure \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-x11-backend \
    --enable-broadway-backend \
    `# --enable-wayland-backend -- Fails to build` \
    --enable-test-print-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/arch_settings.ini" \
    "${pkgdir}/etc/gtk-3.0/settings.ini"
}

# vim:set ts=2 sw=2 et:
