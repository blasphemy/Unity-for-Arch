# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=geis
_actual_ver=2.2.16
_extra_ver=daily13.06.05
pkgver=${_actual_ver}.${_extra_ver}
pkgrel=100
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/geis"
license=('GPL' 'LGPL')
depends=('grail' 'xorg-xinput' 'python3' 'dbus-core')
makedepends=('xorg-server-devel')
options=('!emptydirs' '!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/geis_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('7bafeedb042ea6f40bf3d132f8cba3e120defea3011a486718535bac16380199180c65903bafca59243950a268a68eb82417a9c4ad82ee1b8e06a283171426f3')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error=format="

  autoreconf -vfi

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
