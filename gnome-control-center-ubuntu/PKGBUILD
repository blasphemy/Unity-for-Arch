# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-control-center-ubuntu
_ppa_rel=0ubuntu1~saucy2
_translations=20130418
pkgver=3.10.1
pkgrel=100
pkgdesc="The Control Center for GNOME with Ubuntu's patches"
arch=('i686' 'x86_64')
groups=('gnome')
url="http://www.gnome.org"
license=('GPL')
depends=('accountsservice' 'cups-pk-helper' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'gnome-online-accounts' 'gnome-settings-daemon-ubuntu' 'gsettings-desktop-schemas' 'gtk3-ubuntu' 'libgtop' 'network-manager-applet' 'sound-theme-freedesktop' 'upower' 'libpwquality' 'gnome-color-manager' 'smbclient' 'modemmanager' 'libgnomekbd' 'webkitgtk3' 'system-config-printer')
makedepends=('gnome-common' 'gnome-doc-utils' 'intltool' 'docbook-xsl' 'vala' 'desktop-file-utils')
optdepends=('gnome-user-share: Bluetooth and WebDAV file sharing'
            'rygel: media sharing'
            'vino: screen sharing'
            'openssh: remote login')
provides=("gnome-control-center=${pkgver}" 'FEATURE-libgnome-control-center')
conflicts=('gnome-control-center' 'gnome-control-center-unity' 'credentials-preferences' 'credentials-preferences-ubuntu')
options=('!libtool' '!emptydirs')
install=gnome-control-center.install
source=("http://download.gnome.org/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-control-center/gnome-control-center_${pkgver}-${_ppa_rel}.debian.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-control-center.tar.gz")
sha512sums=('4037bfa9d68781c0c6e66e28557f515a83a4f0800fd227db8b00475643f8e0085b1cd4bb2ad8d98afc6e8a574e0f7d5b85c121c1b0b0adb4860542307231e58e'
            '3e2dbba0862593eb4b6b3d028fc6126873520366cf92ef23734f89b515b6f46788514d8f4ddd5b28af7054becc673f90cba58ce5b0851d84840c23cc361a443b'
            'f08d00d02170d55b9908ae8aef2a13f9cbd393168bc70b28e31e8f35ab690903992ba380afbf7ef935d13ce81d3c3bec9b9db2711e2bbc7d1a245e700df5bdbc')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
    # Disable patches
      # Arch Linux uses PackageKit
        sed -i '/05_run_update_manager.patch/d' "${srcdir}/debian/patches/series"
      # Ubuntu specific
        sed -i '/52_region_language.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/53_use_ubuntu_help.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/62_update_translations_template.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/92_ubuntu_system_proxy.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/revert_git_info_packagekit_api.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/ubuntu_region_packagekit.patch/d' "${srcdir}/debian/patches/series"
      # There are currently no packages compatible with this library (assuming it even works)
        sed -i '/revert_git_drop_library.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  ### Translations are currently incomplete ###

#  msg "Merging translations from ${_translations}"
#  rm -f po/LINGUAS po/*.pot
#  mv "${srcdir}"/po/*.pot po/
#  for i in "${srcdir}"/po/*.po; do
#    FILE=$(sed -n "s|.*/gnome-control-center-2.0-||p" <<< ${i})
#    mv ${i} po/${FILE}
#    echo ${FILE%.*} >> po/LINGUAS
#  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --enable-systemd

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Fix a warning
  chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
  chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Ubuntu specific files
  install -dm755 "${pkgdir}/usr/share/indicators/session/applications/"
  ln -s '/usr/share/applications/gnome-control-center.desktop' \
    "${pkgdir}/usr/share/indicators/session/applications/gnome-control-center.desktop"

  # Install legal notice
  install -m644 "${srcdir}/debian/searchingthedashlegalnotice.html" \
    "${pkgdir}/usr/share/gnome-control-center/"

  # Make the Region panel show up in Unity
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --add-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-region-panel.desktop"

  # Make the Background panel show up in Unity
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --add-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-background-panel.desktop"

  # Make the Online Accounts panel show up in Unity
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --add-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-online-accounts-panel.desktop"

  # Make the Date & Time panel show up in Unity
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --add-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-datetime-panel.desktop"
}

# vim:set ts=2 sw=2 et:
